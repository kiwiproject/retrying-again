/*
 * Copyright 2012-2015 Ray Holder
 * Modifications copyright 2017-2018 Robert Huffman
 * Modifications copyright 2020-2021 Kiwi Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'com.github.ben-manes.versions'

group = 'org.kiwiproject'
version = '0.5.0-SNAPSHOT'
sourceCompatibility = '11'
targetCompatibility = '11'

ext.isReleaseVersion = !version.endsWith('SNAPSHOT')
ext.scmRevision = "git rev-parse HEAD".execute().text.trim()

repositories {
    mavenCentral()
}

ext.junitJupiterVersion = '5.7.0'

dependencies {
    compile 'com.google.guava:guava:30.0-jre'
    compileOnly 'com.google.code.findbugs:jsr305:3.0.2'

    testCompile group: 'org.assertj', name: 'assertj-core', version: '3.18.1'
    testCompile "org.junit.jupiter:junit-jupiter-api:${junitJupiterVersion}"
    testCompile "org.junit.jupiter:junit-jupiter-params:${junitJupiterVersion}"
    testCompile group: 'org.kiwiproject', name: 'kiwi-test', version: '0.12.0'
    testCompile 'org.mockito:mockito-core:3.7.0'

    // To avoid compiler warning about use of experimental features in JUnit 5
    testCompileOnly 'org.apiguardian:apiguardian-api:1.1.0'

    testRuntime "org.junit.jupiter:junit-jupiter-engine:${junitJupiterVersion}"
}

test {
    useJUnitPlatform()
}

tasks.withType(Jar) {
    manifest {
        attributes 'Version': archiveVersion
        attributes 'Timestamp': new Date().toString()
        attributes 'SCM-Revision': scmRevision
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task publishJavadoc(type: Copy, dependsOn: javadoc) {
    from javadoc.destinationDir
    into "$projectDir/docs"
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

signing {
    required { isReleaseVersion && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            def sonatypeUser = getProperty('sonatypeUsername', 'SONATYPE_USERNAME_IS_NOT_SET')
            def sonatypePass = getProperty('sonatypePassword', 'SONATYPE_PASSWORD_IS_NOT_SET')

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonatypeUser, password: sonatypePass)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: sonatypeUser, password: sonatypePass)
            }

            pom.project {
                name = 'retrying-again'
                packaging 'jar'
                description 'A library to create configurable retrying strategies for an arbitrary function call, such as a method that connects to a remote service with flaky uptime.'
                url 'https://github.com/kiwiproject/retrying-again'

                scm {
                    url 'scm:git@github.com:kiwiproject/retrying-again.git'
                    connection 'scm:git@github.com:kiwiproject/retrying-again.git'
                    developerConnection 'scm:git@github.com:kiwiproject/retrying-again.git'
                }

                licenses {
                    license {
                        name = 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'sleberknight'
                        name = 'Scott Leberknight'
                    }
                }
            }
        }
    }
}

wrapper {
    gradleVersion = '6.7.1'
}

String getProperty(String name, String defaultValue) {
    project.hasProperty(name) ? project.getProperty(name) : defaultValue
}

idea {
    module {
        downloadSources = true
    }
    project {
        vcs = 'Git'
    }
}

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "com.github.ben-manes:gradle-versions-plugin:0.36.0"
    }
}
